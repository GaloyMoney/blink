# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is licensed under both the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree and the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree.

load("@prelude//utils:utils.bzl", "flatten")
load("@prelude//python/sourcedb/filter.bxl", "do_filter")

def check_targets(ctx: bxl.Context, targets: typing.Any) -> None:
    checked_targets = ctx.configured_targets(do_filter(ctx.uquery(), targets))

    build_result = ctx.build([
        target.label.with_sub_target("typecheck")
        for target in checked_targets
    ])
    output = ctx.output.ensure_multiple(build_result)

    ctx.output.print_json({
        "artifacts": {
            label.raw_target(): [artifact.rel_path() for artifact in artifacts]
            for label, artifacts in output.items()
        },
        "root": ctx.root(),
    })

def _run_entry_point(ctx: bxl.Context) -> None:
    targets = flatten(ctx.cli_args.target)
    check_targets(ctx, targets)

run = bxl_main(
    doc = "Run [typecheck] on a set of targets or target patterns.",
    impl = _run_entry_point,
    cli_args = {
        "target": cli_args.list(
            cli_args.target_expr(
                doc = "Target pattern to run type checking on",
            ),
        ),
    },
)
