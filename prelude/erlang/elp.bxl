# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is licensed under both the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree and the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree.

# Provide information so that ELP is able to load a BUCK project

load("@prelude//:paths.bzl", "paths")

# ------------------ IMPL ------------------

def _clean_up_includes(includes):
    # - Strip to just dir, not file name
    # - Remove duplicates
    # Note: Sometimes the buck rule generating the includes has an excludes glob for a directory.
    #       This flattening will potentially expose excluded files in the directory.
    #       But we do it, because otherwise the performance in erlang_service parsing is too poor.
    include_paths = [_as_path(paths.dirname(p["value"])) for p in includes if p["type"] == "path"]
    targets = [t for t in includes if t["type"] == "target"]
    return include_paths + targets

def _get_includes(ctx, includes_target):
    return _clean_up_includes([_get_absolute_path(ctx, inc) for inc in includes_target.value()])

def _dep_includes(ctx, dep, target_universe):
    lookup_val = "{}:{}".format(dep.path, dep.name)
    dep_target = target_universe.lookup(lookup_val)
    if not dep_target:
        return []
    dep_target = dep_target[0]
    includes = dep_target.attrs_lazy().get("includes")

    if not includes:
        return []
    else:
        return _get_includes(ctx, includes)

def _get_absolute_path(ctx, src) -> dict[str, str]:
    """
    Get the absolute path of the thing passed in, which is either an artifact or a target label.
    """
    if isinstance(src, ConfiguredProvidersLabel):
        return _as_target(str(src.raw_target()))
    else:
        return _as_path(get_path_without_materialization(src, ctx, abs = True))

def _elp_config(ctx):
    cells = {
        cell: cell_path
        for cell, cell_path in ctx.audit().cell(aliases = True).items()
    }

    included_targets = ctx.cli_args.included_targets
    target_universe = ctx.target_universe(included_targets)

    all = ctx.cquery().kind("^(erlang_app|erlang_test)$", ctx.configured_targets(included_targets))
    if ctx.cli_args.deps_target:
        all += ctx.cquery().kind("^erlang_app$", ctx.configured_targets(ctx.cli_args.deps_target))
    result = {}
    dep_includes_cache = {}  # Cache of includes per dependency
    for target in all:
        label = target.label
        label_name = label.raw_target()
        deps = target.attrs_lazy().get("deps")

        includes = target.attrs_lazy().get("includes")
        if not includes:
            includes = []
        else:
            includes = _get_includes(ctx, includes)

        if ctx.cli_args.deps_includes:
            if deps:
                for dep in deps.value():
                    if dep in dep_includes_cache:
                        dep_includes = dep_includes_cache[dep]
                    else:
                        dep_includes = _dep_includes(ctx, dep, target_universe)
                        dep_includes_cache[dep] = dep_includes
                    includes = includes + dep_includes
            apps = target.attrs_lazy().get("applications")

            if apps:
                for app in apps.value():
                    includes.append(_as_path(str(_file_node_to_path(cells, app.path))))

        srcs = target.attrs_lazy().get("srcs")
        if not srcs:
            srcs = []
        else:
            srcs = [_get_absolute_path(ctx, src) for src in srcs.value()]

        suite = target.attrs_lazy().get("suite")
        if not suite:
            suite = None
        elif suite.value() == None:
            suite = None
        else:
            suite_info = _get_absolute_path(ctx, suite.value())
            if suite_info["type"] == "path":
                suite = suite_info["value"]
            else:
                suite = None

        includes = _build_output(includes)
        srcs = _build_output(srcs)
        result[label_name] = dict(
            name = target.attrs_lazy().get("name"),
            suite = suite,
            srcs = srcs,
            includes = includes,
            labels = target.attrs_lazy().get("labels"),
        )
    ctx.output.print_json(result)

def _file_node_to_path(cells: dict[str, str], file_node) -> str:
    cell, path = str(file_node).split("//", 1)
    return paths.join(cells[cell], path)

def _build_output(items: list[dict[str, str]]) -> list[str]:
    # we completely ignore targets, since we don't have support for generated files in ELP
    paths = _list_dedupe([p["value"] for p in items if p["type"] == "path"])
    return paths

def _list_dedupe(xs: list[str]) -> list[str]:
    return {x: True for x in xs}.keys()

def _as_path(src):
    return {"type": "path", "value": src}

def _as_target(src):
    return {"type": "target", "value": src}

# ------------------ INTERFACE ------------------

elp_config = bxl_main(
    impl = _elp_config,
    cli_args = {
        "deps_includes": cli_args.bool(False, doc = "Use include paths from the dependencies too."),
        "deps_target": cli_args.option(cli_args.string(), doc = "Target to include deps from, if specified. See corresponding field in .elp.toml"),
        "included_targets": cli_args.list(cli_args.string(), doc = "Targets to include in the query. See corresponding field in .elp.toml"),
    },
)

# Run with `buck2 bxl prelude//erlang/elp.bxl:elp_config`
# e.g.
# buck2 bxl prelude//erlang/elp.bxl:elp_config -- --included_targets cell//...
# buck2 bxl prelude//erlang/elp.bxl:elp_config -- --included_targets cell//... --deps_includes true
